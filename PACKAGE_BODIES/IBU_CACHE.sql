--------------------------------------------------------
--  DDL for Package Body IBU_CACHE
--------------------------------------------------------

  CREATE OR REPLACE EDITIONABLE PACKAGE BODY "APPS"."IBU_CACHE" as
/* $Header: ibuxpkgb.pls 115.10 2004/03/29 22:14:47 lahuang ship $ */

/* ------------------------------------------------------------------------- */
procedure timeout_entries(errbuf out NOCOPY varchar2,
                          errcode out NOCOPY number,
                          timeout in number)
as
  cutofftime date;
begin
  cutofftime := sysdate - timeout/1440;
  delete from CS_KB_CACHE where timestamp is not null and
                                timestamp < cutofftime;
  errcode := 0;
  exception
    when others then
      errcode := 2;
      errbuf := SQLERRM;
      raise;
end timeout_entries;

/* ------------------------------------------------------------------------- */
procedure install_timeout_job(msg out NOCOPY varchar2)
as
begin

  msg := '';

  /* Create the executable for the job */

  if fnd_program.executable_exists(
                     executable_short_name => 'IBU_TEXT_CACHE_CLEANUP_EXEC',
                     application => 'IBU')
  then
    msg := msg || 'Executable IBU_TEXT_CACHE_CLEANUP_EXEC already exists.';
  else
    fnd_program.executable(executable => 'IBU_TEXT_CACHE_CLEANUP_EXEC',
                           application => 'IBU',
                           short_name => 'IBU_TEXT_CACHE_CLEANUP_EXEC',
                           description => 'Timeout cache entries for IBU',
                           execution_method => 'PL/SQL Stored Procedure',
                           execution_file_name => 'IBU_CACHE.TIMEOUT_ENTRIES',
                           subroutine_name => null,
                           icon_name => null,
                           language_code => 'US');
    msg := msg ||'Executable IBU_TEXT_CACHE_CLEANUP_EXEC successfully created.';
  end if;

  /* Create the program and its parameter(s) for the job */

  if fnd_program.program_exists(program => 'IBU_TEXT_CACHE_CLEANUP_PROG',
                                application => 'IBU')
  then
    msg := msg || 'Program IBU_TEXT_CACHE_CLEANUP_PROG already exists.';
  else
    fnd_program.register(program => 'Timeout for the cache entries for IBU',
                         application => 'IBU',
                         enabled => 'Y',
                         short_name => 'IBU_TEXT_CACHE_CLEANUP_PROG',
                         description => 'Timeout cache entries for IBU',
                         executable_short_name =>'IBU_TEXT_CACHE_CLEANUP_EXEC',
                         executable_application => 'IBU',
                         execution_options => NULL,
                         priority => NULL,
                         save_output => 'N',
                         print => 'N',
                         cols => NULL,
                         rows => NULL,
                         style => NULL,
                         style_required => 'N',
                         printer => null,
                         request_type => null,
                         request_type_application => null,
                         use_in_srs => 'Y',
                         allow_disabled_values => 'N',
                         run_alone => 'N',
                         output_type => NULL,
                         enable_trace => 'N',
                         restart => 'Y',
                         nls_compliant => 'N',
                         icon_name => NULL,
                         language_code => 'US');

    msg := msg || 'Program IBU_TEXT_CACHE_CLEANUP_PROG successfully created.';

    fnd_program.parameter(program_short_name => 'IBU_TEXT_CACHE_CLEANUP_PROG',
                          application => 'IBU',
                          sequence => 1,
                          parameter => 'TIMEOUT',
                          description => 'Age of entry to expire in minutes.',
                          enabled => 'Y',
                          value_set => '30 Characters Optional',
                          default_type => 'Constant',
                          default_value => '30',
                          required => 'N',
                          enable_security => 'N',
                          range => null,
                          display => 'Y',
                          display_size => 30,
                          description_size => 50,
                          concatenated_description_size => 30,
                          prompt => 'Timeout',
                          token => NULL);
  end if;

  exception
    when program_error then
      /* Load the message generated by fnd_program */
      /* so that we can print it to the log.       */
      msg :=  msg || fnd_program.message;

    when others then
      /* Re-raise the exception and SQL*Plus   */
      /* will print the Oracle error and exit. */
      raise;

end install_timeout_job;

/* ------------------------------------------------------------------------- */
procedure uninstall_timeout_job(msg out NOCOPY varchar2)
as
begin

  msg := '';

  if fnd_program.program_exists(program => 'IBU_TEXT_CACHE_CLEANUP_PROG',
                                application => 'IBU')
  then
    fnd_program.delete_program('IBU_TEXT_CACHE_CLEANUP_PROG', 'IBU');
    msg := msg || 'Program IBU_TEXT_CACHE_CLEANUP_PROG successfully uninstalled.';
  else
    msg := msg || 'Program IBU_TEXT_CACHE_CLEANUP_PROG does not exist.';
  end if;

  if fnd_program.executable_exists('IBU_TEXT_CACHE_CLEANUP_EXEC', 'IBU')
  then
    fnd_program.delete_executable('IBU_TEXT_CACHE_CLEANUP_EXEC', 'IBU');
    msg := msg || 'Executable IBU_TEXT_CACHE_CLEANUP_EXEC successfully uninstalled.';
  else
    msg := msg || 'Executable IBU_TEXT_CACHE_CLEANUP_EXEC does not exist.';
  end if;

end uninstall_timeout_job;


end IBU_CACHE;

/
